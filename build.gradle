import org.gradle.internal.os.OperatingSystem
plugins {
	id "fabric-loom" version "1.6-SNAPSHOT"
	id "legacy-looming" version "1.6-SNAPSHOT" // Version must be the same as fabric-loom's
	id "maven-publish"
}

base.archivesName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://jitpack.io" }
	mavenCentral()
	maven {
		name = "cursed-mappings"
		url = "https://raw.githubusercontent.com/BleachDev/cursed-mappings/main/"
	}
}

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		def osArch = System.getProperty("os.arch")
		if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
			project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
		} else if  (osArch.startsWith("ppc")) {
			project.ext.lwjglNatives += "-ppc64le"
		} else if  (osArch.startsWith("riscv")) {
			project.ext.lwjglNatives += "-riscv64"
		}
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.contains("64")
				? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
				: "natives-windows-x86"
		break
}

loom {
	// Only needed for versions not available from vanilla launcher by default.
	// customMinecraftManifest.set("https://meta.legacyfabric.net/v2/manifest/${minecraft_version}")

	// Required by 1.7.x
	// runs {
	// 	client {
	// 		programArgs "--userProperties", "{}"
	// 	}
	// }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(legacy.yarn(project.minecraft_version, project.yarn_build))
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API provides hooks for events, item registration, and more. As most mods will need this, it's included by default.
	// If you know for a fact you don't, it's not required and can be safely removed.
	modImplementation "net.legacyfabric.legacy-fabric-api:legacy-fabric-api:${project.fabric_version}"

	implementation 'org.reflections:reflections:0.10.2'
	implementation 'org.slick2d:slick2d-core:1.0.1'
	implementation platform("org.lwjgl:lwjgl-bom:3.3.2")

	compileOnly(group: "org.projectlombok", name: "lombok", version: "1.18.24")
	annotationProcessor(group: "org.projectlombok", name: "lombok", version: "1.18.24")

	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-nanovg::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

	implementation "io.waterwave.Legacy-LWJGL3:lwjgl:${project.lwjgl_version}-${project.lwjgl_patch_version}"
	implementation "io.waterwave.Legacy-LWJGL3:lwjgl-stb:${project.lwjgl_version}-${project.lwjgl_patch_version}"
	implementation "io.waterwave.Legacy-LWJGL3:lwjgl-nanovg:${project.lwjgl_version}-${project.lwjgl_patch_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// Select the repositories you want to publish to
	repositories {
		// Uncomment to publish to the local maven
		// mavenLocal()
	}
}
